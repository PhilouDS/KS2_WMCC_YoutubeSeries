// Last modification - KONTROL SYSTEM 2 - v0.5.9.2
//____________________________________________________
// CORE LIBRARIES
//‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾
use { format } from core::str

//____________________________________________________
// KSP LIBRARIES
//‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾
use { CONSOLE } from ksp::console
use { CONSOLE_WINDOW, screen_size, open_window, open_centered_window, Align, Window, Container, StringInputField } from ksp::ui
use { MAINFRAME, Process, sleep, yield, current_time } from ksp::game
use { open_log_file, LogFile, MAIN_LOG, SAVE_LOAD_CONTROL } from ksp::debug
use { Vessel, DeltaVSituation, AutopilotMode, Targetable } from ksp::vessel
use { get_vessels_in_range, get_all_owned_vessels } from ksp::vessel
use { Orbit, Body, find_body } from ksp::orbit

//____________________________________________________
// PERSONAL LIBRARIES
//‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾
use * from lib::misc
use { Stage_Info } from lib::my_structures
use * from lib::mission_logs
use * from lib::my_constants

pub const window_width: float = 500
pub const window_height: float = 250

pub const last_modification: string = "0.5.9.2"


//____________________________________________________
// FUNCTIONS
//‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾‾

//===============================
//-> find specific vessel
//===============================

pub fn find_vessels(info: string, body_name: string = "a") -> Vessel[] = {
  let vessels = get_all_owned_vessels().filter(fn(v) -> v.name.to_lower().contains(info.to_lower()))
  if (find_body(body_name).success) {
    vessels = vessels.filter(fn(v) -> v.main_body.name == body_name)
  }
  vessels
}

//===============================
//-> find specific vessel with UI
//===============================

pub fn choose_vessel (craft: Vessel) -> Vessel = {
  const vessels: Vessel[] = get_all_owned_vessels()
  const names_of_vessels = vessels.sort_by(fn(v) -> v.main_body.name).map(fn(v) -> v.name)
  const names_and_body = vessels.sort_by(fn(v) -> v.main_body.name).map(fn(v) -> $"{v.main_body.name, 10} -- {v.name}")

  const selected_vessel = Cell(craft.name)
  const different_vessel_selected = Cell(false)
  const vessel_window = open_centered_window("<b>CHOOSE VESSEL TO TARGET</b>", window_width, window_height)
  
  vessel_window.add_spacer(5)

  create_version_box(vessel_window, last_modification)

  vessel_window.add_spacer(10)

  const dropdown = vessel_window.add_dropdown(names_and_body)

  vessel_window.add_spacer(10)

  const button_box = vessel_window.add_vertical(gap_box, Align.Stretch)
  
  const validate_button = button_box.add_button("Choose vessel", Align.Stretch)
  validate_button.font_size = button_font_size

  while (!vessel_window.is_closed) { 
    validate_button.on_click(fn() -> {
      vessel_window.close()
    })
    dropdown.on_change(fn(i) -> {
      selected_vessel.value = names_of_vessels[i]
      if (selected_vessel.value != craft.name) {
        different_vessel_selected.value = true
      }
    })
    yield()
  }

  const choice = find_vessels(selected_vessel.value)[0]

  if (different_vessel_selected.value) {
    CONSOLE.print_line($"Targeting the vessel:\n   {choice.name} on {choice.main_body.name}")
  } else {
    CONSOLE.clear()
    console_size(600,400)
    yield()
    CONSOLE.print_line(" === ERROR ===")
    CONSOLE.print_line("   Please select a different vessel or a new script.")
    yield()
    MAINFRAME.available_processes.filter(fn(p) -> (p.name.contains("missions") && p.is_running))[0].stop()
  }

  sleep(1)

  choice
}

//===============================
//-> check for particular parts
//===============================

pub fn check_fairing (craft: Vessel) -> bool = craft.parts.filter_map(fn(p) -> p.fairing).length > 0
pub fn check_clamps (craft: Vessel) -> bool = craft.parts.filter_map(fn(p) -> p.launch_clamp).length > 0

//===============================
//-> Starting the script with or without target
//===============================

pub fn start_with_target (craft: Vessel, title: string, targ: Body, refresh_log: bool = true) -> Body = {
  CONSOLE.clear()
  CONSOLE_WINDOW.open()
  console_size(400,500)
  notif_green("Initialisation of the script")
  CONSOLE.print_line($" => {craft.name} is the active vessel.")
  const this_mission = MAINFRAME.available_processes.filter(fn(p) -> p.is_running)[0]
  sleep(0.5)
  craft.target = targ
  sleep(0.5)
  const ship_target = craft.target.value.body.value
  CONSOLE.print_line($"Target: {ship_target.name}")

  if (refresh_log) {
    MAIN_LOG.truncate()
    log_mission(title, MAIN_LOG) 
    open_log_file("missions_list").log(this_mission.name)
  }  
    
  sleep(0.25)

  CONSOLE.clear()

  craft.autopilot.enabled = false // SAS
  craft.autopilot.mode = AutopilotMode.Autopilot

  ship_target
}

pub fn start (craft: Vessel, title: string, refresh_log: bool = true) -> Unit = {
  CONSOLE.clear()
  CONSOLE_WINDOW.open()
  console_size(400,500)
  notif_green("Initialisation of the script")
  const this_mission = MAINFRAME.available_processes.filter(fn(p) -> p.is_running)[0]
  open_log_file("missions_list").log(this_mission.name)
  sleep(0.5)

  if (refresh_log) {
    MAIN_LOG.truncate()
    log_mission(title, MAIN_LOG) 
  }
    
  sleep(0.25)

  CONSOLE.clear()

  craft.autopilot.enabled = false // SAS
  craft.autopilot.mode = AutopilotMode.Autopilot
}

pub fn go_to_orbit(craft: Vessel, targetApo: float, launcher: string, perc: float = 0.95) -> Unit = {
  MAINFRAME.available_processes.filter(fn(p) -> p.name == "to_orbit")[0].start(craft, [targetApo.to_string(), launcher, perc.to_string()])
  yield()
  MAINFRAME.available_processes.filter(fn(p) -> (p.name.contains("missions") && p.is_running))[0].stop()
}

// //===============================
// //-> logs GT parameters of the craft and returns it
// //===============================

pub fn launcher_parameters (craft: Vessel, launcher: string) -> (pitchAng: float, pitchVel: float) = {
  const crafts_file = open_log_file("launchers")
  const crafts_list = crafts_file.read_lines()
  let new_param = true
  let pitchAng = 0.0
  let pitchVel = 0.0
  let find_lv = false

  for (i in 0..crafts_list.length) {
    if (crafts_list[i].contains(launcher)) {
      new_param = false
      pitchAng = crafts_list[i+1].to_float().value
      pitchVel = crafts_list[i+2].to_float().value
      CONSOLE.print_line("Parameters loaded")
      find_lv = true
      break
    }
  }

  if (!find_lv) {manual_action(craft, "Launcher vehicule not found")}

  (pitchAng: pitchAng, pitchVel: pitchVel)
}

//===============================
//-> countdown
//===============================

pub fn countdown (craft: Vessel, clamps: bool = true, count: int = 3, file: LogFile = MAIN_LOG) -> Unit = {
  CONSOLE.clear_line(4)
  const lift_time = current_time() + 3
  let ignition = false
  while (current_time() < lift_time) {
    CONSOLE.print_at(4,0, $"{"Liftoff in:", 20} {(lift_time - current_time()):N1} s")
    if (!ignition && clamps && current_time() > lift_time - 1) {
      craft.staging.next()
      CONSOLE.print_at(6,0, "Ignition")
      ignition = true
    }
    yield()
  }
  craft.staging.next()
  CONSOLE.clear()
  print_title("LIFTOFF", 0, false)
  yield()
  const this_stage = Stage_Info(craft, craft.staging.current)
  const twr = this_stage.get_twr(DeltaVSituation.Altitude)
  const dv = this_stage.get_total_delta_v(DeltaVSituation.Altitude)
  
  log_mission_event(craft, "Liftoff", file)
  add_item($"Actual TWR    : {twr:N2}", "L", file)
  add_item($"Actual Delta-V: {dv:N2} m/s", "L", file)
  popup("Liftoff")
}


//===============================
//-> Launch with 2 given azimuth, selection of the good one
//===============================

pub fn launch_from_north (craft: Vessel, craft_target: Body, azimuth: float[]) -> float = {
  let angle_from_north = 90.0
  let msg = format("azimuth = {0:N1} °", angle_from_north)
  if (azimuth.length != 0) {
    let azimuthAN = azimuth[0]
    let azimuthDN = azimuth[1]

    const time_AN = craft.orbit.time_of_ascending_node(craft_target.orbit)
    const time_DN = craft.orbit.time_of_descending_node(craft_target.orbit)

    if (time_AN > time_DN) {
      angle_from_north = azimuthAN
      msg = format("azimuth = {0:N1} °", angle_from_north)
    } else {
      angle_from_north = azimuthDN
      msg = format("azimuth = {0:N1} °", angle_from_north)
    }
  }
  CONSOLE.print_line(msg)

  return angle_from_north
}